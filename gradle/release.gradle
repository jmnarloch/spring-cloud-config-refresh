
apply plugin: 'net.researchgate.release'

subprojects {
    
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
    
    publishing {
        publications {
            ProjectPublication(MavenPublication) {
                from components.java
                groupId 'com.github.jmnarloch'
                artifactId project.name
                version rootProject.version

                artifact sourcesJar 
                artifact javadocJar
            }
        }
    }

    bintray {

        user = rootProject.hasProperty('bintray_user') ? rootProject.bintray_user : ''
        key = rootProject.hasProperty('bintray_api_key') ? rootProject.bintray_api_key : ''

        pkg {
            repo = 'maven'
            name = 'spring-cloud-config-refresh'
            publications = ['ProjectPublication'] 
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/jmnarloch/spring-cloud-config-refresh.git'
            version {
                name = rootProject.version
            }
        }
    }

    release {
        tagTemplate = '${version}'

        git {
            requireBranch = 'master'
            pushToRemote = 'origin'
            pushToCurrentBranch = false
        }
    }
    afterReleaseBuild.dependsOn bintrayUpload
}